#version 430

#pragma include "Includes/Configuration.include"
#pragma include "Includes/Structures/VoxelDirections.struct"

uniform isampler3D voxelGenSrcR;
uniform isampler3D voxelGenSrcG;
uniform isampler3D voxelGenSrcB;
// uniform writeonly image3D dest;


#if defined(USE_DEBUG_ATTACHMENTS)
out vec4 color;
#endif

uniform writeonly image3D voxelDataDest0;
uniform writeonly image3D voxelDataDest1;
uniform writeonly image3D voxelDataDest2;
uniform writeonly image3D voxelDataDest3;
uniform writeonly image3D voxelDataDest4;


VoxelDirections unpackData(int packedData) {
    VoxelDirections dir;
    dir.posX = ((packedData >> 0*5) & 0x1F) / 31.0;
    dir.negX = ((packedData >> 1*5) & 0x1F) / 31.0;
    dir.posY = ((packedData >> 2*5) & 0x1F) / 31.0;
    dir.negY = ((packedData >> 3*5) & 0x1F) / 31.0;
    dir.posZ = ((packedData >> 4*5) & 0x1F) / 31.0;
    dir.negZ = ((packedData >> 5*5) & 0x1F) / 31.0;
    return dir;
}



void main() {
    ivec2 coord = ivec2(gl_FragCoord.xy);

    #if defined(USE_DEBUG_ATTACHMENTS)
        vec3 vSum = vec3(0);
    #endif
        
    for (int z = 0; z < GI_GRID_RESOLUTION; z++) {
        ivec3 localCoord = ivec3(coord, z);
       
        int valR = texelFetch(voxelGenSrcR, localCoord, 0).x;
        int valG = texelFetch(voxelGenSrcG, localCoord, 0).x;
        int valB = texelFetch(voxelGenSrcB, localCoord, 0).x;

        VoxelDirections voxelR = unpackData(valR);
        VoxelDirections voxelG = unpackData(valG);
        VoxelDirections voxelB = unpackData(valB);

        #if defined(USE_DEBUG_ATTACHMENTS)
            vSum.x += voxelR.posX * 0.1 + voxelR.negX * 0.1;
            vSum.y += voxelR.posY * 0.1 + voxelR.negY * 0.1;
            vSum.z += voxelR.posZ * 0.1 + voxelR.negZ * 0.1;
        #endif

        float solid = (valR > 1 || valG > 1 || valB > 1) ? 1 : 0; 

        imageStore(voxelDataDest0, localCoord, vec4(voxelR.posX, voxelR.negX, voxelR.posY, voxelR.negY));
        imageStore(voxelDataDest1, localCoord, vec4(voxelR.posZ, voxelR.negZ, voxelG.posX, voxelG.negX));
        imageStore(voxelDataDest2, localCoord, vec4(voxelG.posY, voxelG.negY, voxelG.posZ, voxelG.negZ));
        imageStore(voxelDataDest3, localCoord, vec4(voxelB.posX, voxelB.negX, voxelB.posY, voxelB.negY));
        imageStore(voxelDataDest4, localCoord, vec4(voxelB.posZ, voxelB.negZ, 0, solid));

    }



    #if defined(USE_DEBUG_ATTACHMENTS)
        color = vec4(vSum, 1);
    #endif

}